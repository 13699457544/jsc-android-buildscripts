// Top-level build file where you can add configuration options common to all sub-projects/modules.

project.ext.JSC_ZIP_DIR = "$rootDir/../../dist"
project.ext.JSC_DIR = "$rootDir/../jsc"
def listFiles = new File(project.JSC_ZIP_DIR).listFiles().findAll{ it.name.endsWith('.zip') }
project.ext.JSC_ZIP_FILE = listFiles[listFiles.size() - 1]
project.ext.JSC_NAME = project.JSC_ZIP_FILE.name.take(project.JSC_ZIP_FILE.name.lastIndexOf('.'))
println "\n\n\t\tCompiling using javascript core for android, version ======== ${(char)27}[35m${project.JSC_NAME}${(char)27}[0m =======\n\n"

delete project.JSC_DIR
copy() {
  from zipTree(file(project.JSC_ZIP_FILE))
  into file(project.JSC_DIR)
}

allprojects {
  configurations.all {
    resolutionStrategy {
      force "org.webkit:android-jsc:${project.JSC_NAME}"
    }
  }
}

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }

        // this tells gradle where android-jsc resides
        maven {
            url project.JSC_DIR
        }
    }
}
